apply plugin: 'com.android.application'

// The directory where Engage-specific items are stored
def engageEngineDir = "libs/"

// The version of the Engage Engine we want to use
def engageEngineVersion = "1.118.8877"

// The build type of the Engage Engine we want
def engageEngineBuildType = "release"

// Addition flavors that we want to build for
def additionalFlavorsFile = "flavors/flavors.gradle"

android {
    flavorDimensions "version"

    productFlavors {
        // This is the Rally Tactical Systems "generic" flavor
        generic {
            dimension "version"
            applicationIdSuffix ".generic"
            versionNameSuffix "-generic"
        }
    }

    // Add additional OEM flavors if any
    try {
        apply from: additionalFlavorsFile
    }
    catch(ignored) {
    }

    compileSdkVersion 28

    defaultConfig {
        applicationId "com.rallytac.engageandroid"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 118
        versionName "1.118"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }


    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def apkBaseName = variant.productFlavors.get(0).name
            def SEP = "_"
            def buildType = variant.variantData.variantConfiguration.buildType.name
            def version = variant.versionCode
            def date = new Date();
            def formattedDate = date.format('yyyyMMdd_HHmmss')
            def newApkName = apkBaseName + SEP + version + SEP + buildType + SEP + formattedDate + ".apk"

            outputFileName = new File(newApkName)
        }
    }
}

dependencies {
    // Engage Engine
    implementation fileTree(dir: engageEngineDir, include: ["engage-engine-" + engageEngineBuildType + "-" + engageEngineVersion + ".aar"])

    // Android goodies
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:preference-v7:28.0.0'
    implementation 'com.android.support:support-vector-drawable:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.android.support:support-v4:28.0.0'
    implementation 'com.android.support:design:28.0.0'

    // QR codes
    implementation 'com.journeyapps:zxing-android-embedded:3.5.0'

    // Google Maps
    implementation 'com.google.android.gms:play-services-maps:16.1.0'
    implementation 'com.google.android.gms:play-services-location:16.0.0'
    implementation 'com.google.android.gms:play-services-maps:16.1.0'


    // Charting
    implementation 'com.github.PhilJay:MPAndroidChart:v3.1.0'

    // Testing
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}
